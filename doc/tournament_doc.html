<h3>TABLE OF CONTENTS</h3>
<ul>
<li>1. <a href="#robo0">tournament/file_header</a></li>
<li>2. <a href="#robo1">tournament/init_tourn</a></li>
<ul>
<li>2.1. <a href="#robo2">init_tourn/get_filenames_tourn</a></li>
<li>2.2. <a href="#robo3">init_tourn/init_gui_tourn</a></li>
<ul>
<li>2.2.1. <a href="#robo4">init_gui_tourn/create_tournctrl</a></li>
</ul>
<li>2.3. <a href="#robo5">init_tourn/build_matchlist</a></li>
<li>2.4. <a href="#robo9">init_tourn/run_tourn</a></li>
<ul>
<li>2.4.1. <a href="#robo11">run_tourn/sort_score</a></li>
<li>2.4.2. <a href="#robo12">run_tourn/report_score</a></li>
</ul>
</ul>
<li>3. <a href="#robo6">tournament/update_tourn</a></li>
<ul>
<li>3.1. <a href="#robo7">update_tourn/show_score</a></li>
<li>3.2. <a href="#robo8">update_tourn/show_matches</a></li>
</ul>
<li>4. <a href="#robo10">halt_tourn/reset_tourn</a></li>
</ul>
<a name="robo0"></a>
<a name="tournament2ffile5fheader"></a>
<h1>1.  tournament/file_header [ file ]  </h1>
<p class="item_name">NAME</p>
<p>   tournament.tcl
</p>
<p class="item_name">DESCRIPTION</p>
<p>   This file defines the functionality and GUI description of the
   TclRobots tournament mode.
</p>

<p>   It runs round-robin one-on-one battles until all robots have battled
   every other robot once. Scores are awarded for a win (3p) and a tie
   (1p). At the end of the tournament the scores are compared to find a
   winner.
</p>

<p>   The GUI is the same as single battle mode, with the tournament score
   added.
</p>

<p>   The authors are Jonas Ferry, Peter Spjuth and Martin Lindskog, based
   on TclRobots 2.0 by Tom Poindexter.
</p>

<p>   See http://tclrobots.org for more information.
</p>
<p class="item_name">COPYRIGHT</p>
<p>   Jonas Ferry (jonas.ferry@tclrobots.org), 2010. Licensed under the
   Simplified BSD License. See LICENSE file for details.
</p>
<a name="robo1"></a>
<a name="tournament2finit5ftourn"></a>
<h1>2.  tournament/init_tourn [ procedure ]  </h1>
<p class="item_name">NAME</p>
<p>   init_tourn
</p>
<p class="item_name">DESCRIPTION</p>
<p>   This procedure starts the tournament.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc init_tourn {} {
    global allRobots allRobots_tourn activeRobots activeRobots_tourn data \
        data_tourn game score

    # Clear any old data
    array unset data

    if {$::gui} {
        get_filenames_tourn
        init_gui_tourn
    }
    # Init all robots, can't use init_game from tclrobots.tcl because
    # interpreters should be initialised separately in tournament mode
    init_game base
    init_robots

    if {$::gui} {
        # Init robots on GUI
        gui_init_robots

        # Remove canvas items; these will be initialised again for each match
        $::arena_c delete robot
        $::arena_c delete scan
    }
    foreach robot $allRobots {
        set score($robot) 0
    }
    # Remember allRobots, activeRobots and data
    set allRobots_tourn    $allRobots
    set activeRobots_tourn $activeRobots
    array set data_tourn   [array get data]

    build_matchlist

    if {$::debug} {
        #set ::matchlist {{r0 r1} {r0 r1} {r0 r1}}
    }

    if {$::gui} {
        set game(state) "halt"

        set ::matchnum 0
        set ::tournRanking $allRobots_tourn
        set ::tournScore {}
        foreach robot $allRobots_tourn {
            lappend ::tournScore \
                "[format %3d $score($robot)] $data($robot,name)"
        }
        update_tourn
    }
    # Figure out the longest robot name to line up the report nicely
    set ::long_name 0
    foreach name [array names data *,name] {
        if {[string length $data($name)] &gt; $::long_name} {
            set ::long_name [string length $data($name)]
        }
    }
}
</pre>
<a name="robo2"></a>
<a name="init5ftourn2fget5ffilenames5ftourn"></a>
<h2>2.1.  init_tourn/get_filenames_tourn [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   get_filenames_tourn
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Gets the robot filenames from the file list window.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc get_filenames_tourn {} {
    # get robot filenames from window
    set ::robotFiles $::robotList
}
</pre>
<a name="robo3"></a>
<a name="init5ftourn2finit5fgui5ftourn"></a>
<h2>2.2.  init_tourn/init_gui_tourn [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   init_gui_tourn
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Creates theemacs -r ~/Desktop/code/tcl/tclrobots/tclrobots/tclrobots.tcl tournament mode GUI.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc init_gui_tourn {} {
    grid forget $::sel_f

    show_arena

    # Create and grid the tournament control box
    create_tournctrl

    # Clear message boxes
    set ::robotHealth {}
    set ::robotMsg    {}

    # start robots
    set ::StatusBarMsg "Optionally select match time and outfile and press START"
    button_state "game" run_tourn reset_tourn
}
</pre>
<a name="robo4"></a>
<a name="init5fgui5ftourn2fcreate5ftournctrl"></a>
<h3>2.2.1.  init_gui_tourn/create_tournctrl [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   create_tournctrl
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Create and grid the tournament control box.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc create_tournctrl {} {
    global tourn_f

    # The single battle mode shows the arena, the health box and the
    # message box
    grid $::game_f  -column 0 -row 2 -sticky nsew
    grid $::arena_c -column 0 -row 0 -rowspan 2 -sticky nsew

    set  tourn_f [ttk::frame $::game_f.tourn]
    grid $tourn_f -column 1 -row 0 -sticky nsew

    create_health_msg $tourn_f

    set tournctrl1_f [ttk::frame $tourn_f.time -relief raised -borderwidth 2]
    set tourntime_l  [ttk::label $tourn_f.time.l \
                          -text "Max minutes / match:"]
    set tourntime_e  [ttk::entry $tourn_f.time.e \
                          -textvariable tlimit]

    grid $tourntime_l -column 0 -row 0 -sticky nsew
    grid $tourntime_e -column 0 -row 1 -sticky nsew

    set ::tournScore    {}
    set ::tournScore_lb   [listbox $tourn_f.score -background black \
                               -listvariable ::tournScore]

    set ::tournMatches  {}
    set ::tournMatches_lb [listbox $tourn_f.match -background black \
                               -foreground white -listvariable ::tournMatches]

    set tournctrl2_f [ttk::frame $tourn_f.file -relief raised -borderwidth 2]
    set tournfile_l  [ttk::label $tourn_f.file.l \
                          -text "Optional results filename:"]
    set tournfile_e  [ttk::entry $tourn_f.file.e \
                          -textvariable outfile]

    grid $tournfile_l -column 0 -row 0 -sticky nsew
    grid $tournfile_e -column 0 -row 1 -sticky nsew

    # Fix resizing of widgets
    grid columnconfigure $::game_f 0 -weight 1
    grid columnconfigure $::game_f 1 -weight 1
    grid rowconfigure    $::game_f 0 -weight 1
    grid columnconfigure $tourn_f 0 -weight 1
    grid columnconfigure $tourn_f 1 -weight 1
    grid rowconfigure    $tourn_f  0 -weight 1
    grid rowconfigure    $tourn_f  1 -weight 1
    grid rowconfigure    $tourn_f  2 -weight 1

    # Grid all major widgets
    grid $::robotHealth_lb  -column 0 -row 0 -sticky nsew
    grid $::tournScore_lb   -column 0 -row 1 -sticky nsew
    grid $::tournMatches_lb -column 0 -row 2 -sticky nsew
    grid $tournctrl1_f      -column 0 -row 3 -sticky nsew
    grid $::robotMsg_lb     -column 1 -row 0 -sticky nsew -rowspan 3
    grid $tournctrl2_f      -column 1 -row 3 -sticky nsew
}
</pre>
<a name="robo5"></a>
<a name="init5ftourn2fbuild5fmatchlist"></a>
<h2>2.3.  init_tourn/build_matchlist [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   build_matchlist
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Builds the list of matches in the tournament. Makes sure robots do
   not fight themselves or multiple times against the same opponent.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc build_matchlist {} {
    global allRobots matchlist

    set matchlist {}
    foreach robot $allRobots {
        foreach target $allRobots {
            # Make sure all matches are unique
            if {[&lt;= [lsearch $allRobots $target] \
                     [lsearch $allRobots $robot]]} {
                continue
            }
            lappend matchlist [list $robot $target]
        }
    }
}
</pre>
<a name="robo9"></a>
<a name="init5ftourn2frun5ftourn"></a>
<h2>2.4.  init_tourn/run_tourn [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   run_tourn
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Runs the tournament.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc run_tourn {} {
    global activeRobots activeRobots_tourn allRobots allRobots_tourn \
        data data_tourn game matchlist matchlog score

    if {$::gui} {
        button_state "running"
    }
    set matchlog ""
    puts "MATCHES:\n"

    foreach match $matchlist {
        if {$::gui} {
            # Remove old canvas items
            $::arena_c delete robot
            $::arena_c delete scan
            debug "Running match $::matchnum $match from $matchlist"
        }
        set robot  [lindex $match 0]
        set target [lindex $match 1]

        # Switch all and active robots to current tournament pair
        set allRobots    "$robot $target"
        set activeRobots $allRobots

        # Unset old data array, but retain some information
        array unset data
        foreach robot $allRobots_tourn {
            set data($robot,code)       $data_tourn($robot,code)
            set data($robot,name)       $data_tourn($robot,name)
            set data($robot,num)        $data_tourn($robot,num)
            set data($robot,color)      $data_tourn($robot,color)
            set data($robot,brightness) $data_tourn($robot,brightness)
        }
        # Init current two robots' interpreters
        init_game match

        if {$::gui} {
            foreach robot $allRobots {
                gui_create_robot $robot $data_tourn($robot,color) \
                    [lsearch -exact $allRobots_tourn $robot]
            }
        }
        set game(state) "run"

        coroutine run_robotsCo run_robots

        while {$game(state) eq "run" || $game(state) eq "pause"} {
            vwait game(state)
        }
        if {$game(state) ne "halt"} {
            debug "hello"
            # Set match score for tournament mode
            # Do not score tournament if it was halted
            set match_msg ""
            # Fix padding
            for {set i [string length $data($robot,name)]} \
                {$i &lt;= $::long_name} {incr i} {
                    append match_msg " "
                }
            if {[llength $activeRobots] == 1} {
                incr score([lindex $activeRobots 0]) 3
                if {$robot eq $activeRobots} {
                    append match_msg \
                        "$data($robot,name)(w) vs $data($target,name)"
                } else {
                    append match_msg \
                        "$data($robot,name)    vs $data($target,name)(w)"
                }
            } else {
                # Note that this presupposes two-robot matches
                foreach robot $allRobots {
                    incr score($robot) 1
                }
                append match_msg \
                    "$data($robot,name)    vs $data($target,name) (tie)"
            }
            sort_score

            if {$::gui} {
                update_tourn
                incr ::matchnum
            }
            puts $match_msg
            append matchlog "$match_msg\n"
            }

        # Disable robots and clear messages
        foreach robot $activeRobots {
            disable_robot $robot
            set ::robotMsg {}
        }
        button_state "file"
        report_score
        }
}
</pre>
<a name="robo11"></a>
<a name="run5ftourn2fsort5fscore"></a>
<h3>2.4.1.  run_tourn/sort_score [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   sort_score
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Sorts tournament scores.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc sort_score {} {
    global allRobots_tourn score tournRanking tournScore data

    set scores {}
    foreach robot $allRobots_tourn {
        lappend scores "$robot $score($robot)"
    }

    set tournRanking {}
    set tournScore   {}
    foreach robotscore [lsort -integer -index 1 \
                            -decreasing $scores] {
        set robot [lindex $robotscore 0]
        lappend tournRanking $robot
        lappend tournScore "[format %3d $score($robot)] $data($robot,name)"
    }
}
</pre>
<a name="robo12"></a>
<a name="run5ftourn2freport5fscore"></a>
<h3>2.4.2.  run_tourn/report_score [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   report_score
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Displays scores and if requested reports them to file.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc report_score {} {
    global game matchlog tournScore

    set ::win_msg "TOURNAMENT SCORES:\n\n"
    foreach robotscore $tournScore {
        append ::win_msg "$robotscore\n"
    }
    # show results
    if {$::gui} {
        if {$game(state) eq "halt"} {
            set ::StatusBarMsg "Battle halted"
        } else {
            tk_dialog2 .winner "Results" $::win_msg "-image iconfn" 0 dismiss
        }
    } else {
        puts "\n$::win_msg"
    }
    # Set up report file message
    set outmsg ""
    append outmsg "MATCHES:\n$matchlog\n"
    append outmsg "$::win_msg"

    if {$game(outfile) ne ""} {
        debug "$game(outfile) :::: $outmsg"
        catch {write_file $game(outfile) $outmsg}
    }
}
</pre>
<a name="robo6"></a>
<a name="tournament2fupdate5ftourn"></a>
<h1>3.  tournament/update_tourn [ procedure ]  </h1>
<p class="item_name">NAME</p>
<p>   update_tourn
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc update_tourn {} {
    show_score
    show_matches
    update
}
</pre>
<a name="robo7"></a>
<a name="update5ftourn2fshow5fscore"></a>
<h2>3.1.  update_tourn/show_score [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   show_score
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc show_score {} {
    global tournRanking data tournScore_lb

    set index 0
    foreach robot $tournRanking {
        $tournScore_lb itemconfigure $index -foreground $data($robot,color)
        if {$data($robot,brightness) &gt; 0.5} {
            $tournScore_lb itemconfigure $index -background black
        }
        incr index
    }
}
</pre>
<a name="robo8"></a>
<a name="update5ftourn2fshow5fmatches"></a>
<h2>3.2.  update_tourn/show_matches [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   show_matches
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc show_matches {} {
    set ::tournMatches {}
    set index 0

    foreach match $::matchlist {
        lappend ::tournMatches "$::data([lindex $match 0],name) vs $::data([lindex $match 1],name)"

        if {$::matchnum == $index} {
            # Highlight current match
            $::tournMatches_lb itemconfigure $index -background white
            $::tournMatches_lb itemconfigure $index -foreground black

            if {$index &gt; 0} {
                # Remove highlight from previous match
                $::tournMatches_lb itemconfigure [- $index 1] \
                    -background black
                $::tournMatches_lb itemconfigure [- $index 1] \
                    -foreground white
            }
        }
        incr index
    }
}
</pre>
<a name="robo10"></a>
<a name="halt5ftourn2freset5ftourn"></a>
<h1>4.  halt_tourn/reset_tourn [ procedure ]  </h1>
<p class="item_name">NAME</p>
<p>   reset_tourn
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Reset to file select state.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc reset_tourn {} {
    global game

    set ::StatusBarMsg "Cleaning up"
    update

    set game(state) "halt"
    destroy $::tourn_f

    foreach robot $::activeRobots {
        disable_robot $robot
    }
    if {$::parms(tkp)} {
        $::arena_c delete {*}[$::arena_c children 0]
    } else {
        $::arena_c delete all
    }
    grid forget $::game_f
    grid $::sel_f -column 0 -row 2 -sticky nsew

    button_state "file"
    debug button_state
}
</pre>

