<h3>TABLE OF CONTENTS</h3>
<ul>
<li>1. <a href="#robo0">game/file_header</a></li>
<li>2. <a href="#robo1">game/run_robots</a></li>
<ul>
<li>2.1. <a href="#robo2">run_robots/act</a></li>
<li>2.2. <a href="#robo3">run_robots/update_robots</a></li>
<ul>
<li>2.2.1. <a href="#robo4">update_robots/check_missiles</a></li>
<li>2.2.2. <a href="#robo5">update_robots/update_missile_location</a></li>
<li>2.2.3. <a href="#robo6">update_robots/missile_reached_target</a></li>
<li>2.2.4. <a href="#robo7">update_robots/assign_missile_damage</a></li>
<li>2.2.5. <a href="#robo8">update_robots/check_barrel</a></li>
<li>2.2.6. <a href="#robo9">update_robots/check_speed</a></li>
<li>2.2.7. <a href="#robo10">update_robots/update_speed</a></li>
<li>2.2.8. <a href="#robo11">update_robots/update_heading</a></li>
<li>2.2.9. <a href="#robo12">update_robots/update_distance</a></li>
<li>2.2.10. <a href="#robo13">update_robots/check_wall</a></li>
<li>2.2.11. <a href="#robo14">update_robots/check_health</a></li>
<ul>
<li>2.2.11.1. <a href="#robo15">check_health/disable_robot</a></li>
</ul>
</ul>
<li>2.3. <a href="#robo16">run_robots/tick</a></li>
</ul>
</ul>
<a name="robo0"></a>
<a name="game2ffile5fheader"></a>
<h1>1.  game/file_header [ file ]  </h1>
<p class="item_name">NAME</p>
<p>   game.tcl
</p>
<p class="item_name">DESCRIPTION</p>
<p>   This file contains the game logic.
</p>

<p>   The authors are Jonas Ferry, Peter Spjuth and Martin Lindskog, based
   on TclRobots 2.0 by Tom Poindexter.
</p>

<p>   See http://tclrobots.org for more information.
</p>
<p class="item_name">COPYRIGHT</p>
<p>   Jonas Ferry (jonas.ferry@tclrobots.org), 2010. Licensed under the
   Simplified BSD License. See LICENSE file for details.
</p>
<a name="robo1"></a>
<a name="game2frun5frobots"></a>
<h1>2.  game/run_robots [ procedure ]  </h1>
<p class="item_name">NAME</p>
<p>   run_robots
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc run_robots {} {
    global activeRobots data do_step game gui parms sim_syscall StatusBarMsg \
    step tick timeAt5

    while {$game(state) eq "run" || $game(state) eq "pause"} {
        if {$game(state) ne "pause"} {
            if {$game(simulator)} {
                # Reset health of target
                set data(target,health) [* $parms(health) 10]
            }
            foreach robot $activeRobots {
                if {($data($robot,alert) ne {}) &amp;&amp; \
                        ($data($robot,ping) ne {})} {
                    # Prepend alert data to sysreturn no notify robot it's
                    # been scanned.
                    set data($robot,sysreturn,[- $tick 1]) \
                        "alert $data($robot,alert) $data($robot,ping) $data($robot,sysreturn,[- $tick 1])"

                    # Robot is notified; reset alert request
                    set data($robot,ping) {}
                }
                ${robot}Run $data($robot,sysreturn,[- $tick 1])
            }
            act

            # Print extra information in simulator GUI
            if {$game(simulator) &amp;&amp;
                [ne $data(r0,sysreturn,$tick) ""]} {
                set sim_syscall $data(r0,syscall,$tick)
                append sim_syscall " =&gt; " $data(r0,sysreturn,$tick)
            }
            update_robots

            if {$gui} {
                update_gui
            }
            tick

            # Check if single step is active in simulator mode
            if {$game(simulator) &amp;&amp; $step} {
                vwait do_step
                set do_step 0
            }
            if {$parms(tick) &lt; 5} {
                # Don't bother at high speed
                after $parms(tick) [info coroutine]
            } elseif {$tick &lt;= 5} {
                # Let the first few ticks pass before measuring
                after $parms(tick) [info coroutine]
                set timeAt5 [clock milliseconds]
            } else {
                # Try to measure time, to adjust the tick delay for load
                set target [expr {$parms(tick) * ($tick - 4) + $timeAt5}]
                set delay [expr {$target - [clock milliseconds]}]
                # Sanity check value
                if {$delay &gt; $parms(tick)} {
                    set delay $parms(tick)
                } elseif {$delay &lt; 5} {
                    set delay 5
                }
                after $delay [info coroutine]
                # Keep the lag visible
                set StatusBarMsg "Running $delay"
            }
            yield
        } else {
            # Game is paused, but GUI needs to respond
            update
        }
    }
    rename [info coroutine] ""
    yield
}
</pre>
<a name="robo2"></a>
<a name="run5frobots2fact"></a>
<h2>2.1.  run_robots/act [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   act
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc act {} {
    global activeRobots data tick

    foreach robot $activeRobots {
        if {$data($robot,status)} {
            set currentSyscall $data($robot,syscall,$tick)
            switch [lindex $currentSyscall 0] {
                scanner      {sysScanner     $robot}
                dsp          {sysDsp         $robot}
                alert        {sysAlert       $robot}
                cannon       {sysCannon      $robot}
                drive        {sysDrive       $robot}
                health       -
                speed        -
                heat         -
                loc_x        -
                loc_y        {sysData        $robot}
                tick         {sysTick        $robot}
                team_declare {sysTeamDeclare $robot}
            }
        }
    }
}
</pre>
<a name="robo3"></a>
<a name="run5frobots2fupdate5frobots"></a>
<h2>2.2.  run_robots/update_robots [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   update_robots
</p>
<p class="item_name">DESCRIPTION</p>
<p>   update position of missiles and robots, assess damage
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc update_robots {} {
    global allRobots data game

    foreach robot $allRobots {
        # check all flying missiles
        set num_miss [check_missiles $robot]

        # skip rest if robot dead
        if {!$data($robot,status)} {
            continue
        }
        # update missile reloader
        if {$data($robot,reload)} {
            incr data($robot,reload) -1
        }
        # check for barrel overheat, apply cooling
        check_barrel $robot

        # check for excessive speed, increment heat
        check_speed $robot

        # update robot speed, moderated by acceleration
        update_speed $robot

        # update robot heading, moderated by turn rates
        update_heading $robot

        # update distance traveled on this heading
        update_distance $robot

        # check for wall collision
        check_wall $robot
    }
    # check for robot health
    set health_status [check_health]

    set num_rob       [lindex $health_status 0]
    set diffteam      [lindex $health_status 1]
    set num_team      [lindex $health_status 2]

    if {($num_rob&lt;=1 || $num_team==1) &amp;&amp; $num_miss==0} {
        # Stop game
        set game(state) "end"
    }
}
</pre>
<a name="robo4"></a>
<a name="update5frobots2fcheck5fmissiles"></a>
<h3>2.2.1.  update_robots/check_missiles [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   check_missiles
</p>
<p class="item_name">DESCRIPTION</p>
<p>   check all flying missiles
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc check_missiles {robot} {
    global activeRobots data 
    set num_miss 0

    if {$data($robot,mstate)} {
        incr num_miss
        update_missile_location $robot
        # check if missile reached target
        if {$data($robot,mrange) &gt; $data($robot,mdist)} {
            missile_reached_target $robot

            # assign damage to all within explosion ranges
            foreach target $activeRobots {
                if {!$data($target,status)} {
                    continue
                }
                assign_missile_damage $robot $target
            }
        }
    }
    return $num_miss
}
</pre>
<a name="robo5"></a>
<a name="update5frobots2fupdate5fmissile5flocation"></a>
<h3>2.2.2.  update_robots/update_missile_location [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   update_missile_location
</p>
<p class="item_name">DESCRIPTION</p>
<p>   update location of missile
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc update_missile_location {robot} {
    global c_tab data parms s_tab

    set data($robot,mrange) \
        [+ $data($robot,mrange) $parms(msp)]
    set data($robot,mx) \
        [+ [* $c_tab($data($robot,mhdg)) $data($robot,mrange)] \
             $data($robot,morgx)]
    set data($robot,my) \
        [+ [* $s_tab($data($robot,mhdg)) $data($robot,mrange)] \
             $data($robot,morgy)]
}
</pre>
<a name="robo6"></a>
<a name="update5frobots2fmissile5freached5ftarget"></a>
<h3>2.2.3.  update_robots/missile_reached_target [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   missile_reached_target
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc missile_reached_target {robot} {
    global c_tab data gui s_tab

    set data($robot,mstate) 0
    set data($robot,mx) \
        [+ [* $c_tab($data($robot,mhdg)) $data($robot,mdist)] \
             $data($robot,morgx)]
    set data($robot,my) \
        [+ [* $s_tab($data($robot,mhdg)) $data($robot,mdist)] \
             $data($robot,morgy)]
    if {$gui} {
        after 1 "show_explode $robot"
    }
}
</pre>
<a name="robo7"></a>
<a name="update5frobots2fassign5fmissile5fdamage"></a>
<h3>2.2.4.  update_robots/assign_missile_damage [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   assign_missile_damage
</p>
<p class="item_name">DESCRIPTION</p>
<p>   assign damage to all within explosion ranges
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc assign_missile_damage {robot target} {
    global data parms

    set d [hypot [- $data($robot,mx) $data($target,x)] \
            [- $data($robot,my) $data($target,y)]]
    if {$d &lt; $parms(dia3)} {
        if {$d &lt; $parms(dia0)} {
            incr data($target,health) $parms(hit0)
            incr data($robot,inflicted) [- $parms(hit0)]
        } elseif {$d&lt;$parms(dia1)} {
            incr data($target,health) $parms(hit1)
            incr data($robot,inflicted) [- $parms(hit1)]
        } elseif {$d&lt;$parms(dia2)} {
            incr data($target,health) $parms(hit2)
            incr data($robot,inflicted) [- $parms(hit2)]
        } else {
            incr data($target,health) $parms(hit3)
            incr data($robot,inflicted) [- $parms(hit3)]
        }
    }
}
</pre>
<a name="robo8"></a>
<a name="update5frobots2fcheck5fbarrel"></a>
<h3>2.2.5.  update_robots/check_barrel [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   check_barrel
</p>
<p class="item_name">DESCRIPTION</p>
<p>   check for barrel overheat, apply cooling
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc check_barrel {robot} {
    global data parms

    if {$data($robot,btemp)} {
        incr data($robot,btemp) $parms(cancool)
        if {$data($robot,btemp) &lt; 0} {
            set data($robot,btemp) 0
        }
    }
}
</pre>
<a name="robo9"></a>
<a name="update5frobots2fcheck5fspeed"></a>
<h3>2.2.6.  update_robots/check_speed [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   check_speed
</p>
<p class="item_name">DESCRIPTION</p>
<p>   check for excessive speed, increment heat
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc check_speed {robot} {
    global data parms

    if {$data($robot,speed) &gt; $parms(heatsp)} {
        incr data($robot,heat) \
            [+ [round [/ [- $data($robot,speed) $parms(heatsp)] \
                           $parms(hrate)]] 1]
        if {$data($robot,heat) &gt;= $parms(heatmax)} {
            set data($robot,heat) $parms(heatmax)
            set data($robot,hflag) 1
            if {$data($robot,dspeed) &gt; $parms(heatsp)} {
                set data($robot,dspeed) $parms(heatsp)
            }
        }
    } else {
        # if overheating, apply cooling rate
        if {$data($robot,hflag) || $data($robot,heat) &gt; 0} {
            incr data($robot,heat) $parms(cooling)
            if {$data($robot,heat) &lt;= 0} {
                set data($robot,hflag) 0
                set data($robot,heat) 0
            }
        }
    }
}
</pre>
<a name="robo10"></a>
<a name="update5frobots2fupdate5fspeed"></a>
<h3>2.2.7.  update_robots/update_speed [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   update_speed
</p>
<p class="item_name">DESCRIPTION</p>
<p>   update robot speed, moderated by acceleration
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc update_speed {robot} {
    global data parms

    if {$data($robot,speed) != $data($robot,dspeed)} {
        if {$data($robot,speed) &gt; $data($robot,dspeed)} {
            incr data($robot,speed) -$parms(accel)
            if {$data($robot,speed) &lt; $data($robot,dspeed)} {
                set data($robot,speed) $data($robot,dspeed)
            }
        } else {
            incr data($robot,speed) $parms(accel)
            if {$data($robot,speed) &gt; $data($robot,dspeed)} {
                set data($robot,speed) $data($robot,dspeed)
            }
        }
    }
}
</pre>
<a name="robo11"></a>
<a name="update5frobots2fupdate5fheading"></a>
<h3>2.2.8.  update_robots/update_heading [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   update_heading
</p>
<p class="item_name">DESCRIPTION</p>
<p>   update robot heading, moderated by turn rates
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc update_heading {robot} {
    global data parms

    if {$data($robot,hdg) != $data($robot,dhdg)} {
        set mrate $parms(rate,[int [/ $data($robot,speed) 25]])
        set d1 [% [+ [- $data($robot,dhdg) $data($robot,hdg)]  360] 360]
        set d2 [% [+ [- $data($robot,hdg)  $data($robot,dhdg)] 360] 360]

        if {$d1 &lt; $d2} {
            set d $d1
        } else {
            set d $d2
        }
        if {$d &lt;= $mrate} {
            set data($robot,hdg) $data($robot,dhdg)
        } else {
            set data($robot,hdg) \
                [% [+ [$data($robot,dir) $data($robot,hdg) $mrate] 360] 360]
        }
        set data($robot,orgx)  $data($robot,x)
        set data($robot,orgy)  $data($robot,y)
        set data($robot,range) 0
    }
}
</pre>
<a name="robo12"></a>
<a name="update5frobots2fupdate5fdistance"></a>
<h3>2.2.9.  update_robots/update_distance [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   update_distance
</p>
<p class="item_name">DESCRIPTION</p>
<p>   update distance traveled on this heading
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc update_distance {robot} {
    global c_tab data parms s_tab

    if {$data($robot,speed) &gt; 0} {
        set data($robot,range) \
            [+ $data($robot,range) \
                 [/ [* $data($robot,speed) $parms(sp)] 100]]

        # Modify range with random factor to avoid totally
        # deterministic movement. Range is currently +- 1%.
        # Playtesting will tell if this should be lower or higher.
        set randfactor [/ [+ [mrand 100] 1.0] 10000]
        if {[mrand 2] == 0} {
            set randfactor [- $randfactor]
        }
        set data($robot,range) [+ $data($robot,range) \
                                    [* $data($robot,range) \
                                         $randfactor]]

        set data($robot,x) \
            [round [+ [* $c_tab($data($robot,hdg)) $data($robot,range)] \
                         $data($robot,orgx)]]

        set data($robot,y) \
            [round [+ [* $s_tab($data($robot,hdg)) $data($robot,range)] \
                        $data($robot,orgy)]]
    }
}
</pre>
<a name="robo13"></a>
<a name="update5frobots2fcheck5fwall"></a>
<h3>2.2.10.  update_robots/check_wall [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   check_wall
</p>
<p class="item_name">DESCRIPTION</p>
<p>   check for wall collision
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc check_wall {robot} {
    global data parms

    if {$data($robot,speed) &gt; 0} {
        if {($data($robot,x) &lt; 0) || ($data($robot,x) &gt; 999)} {
            if {$data($robot,x) &lt; 0} {
                set data($robot,x) 0
            } else {
                set data($robot,x) 999
            }
            set data($robot,orgx)    $data($robot,x)
            set data($robot,orgy)    $data($robot,y)
            set data($robot,range)   0
            set data($robot,speed)   0
            set data($robot,dspeed)  0
            incr data($robot,health) $parms(coll)
        }
        if {($data($robot,y) &lt; 0) || ($data($robot,y) &gt; 999)} {
            if {$data($robot,y) &lt; 0} {
                set data($robot,y) 0
            } else {
                set data($robot,y) 999
            }
            set data($robot,orgx)    $data($robot,x)
            set data($robot,orgy)    $data($robot,y)
            set data($robot,range)   0
            set data($robot,speed)   0
            set data($robot,dspeed)  0
            incr data($robot,health) $parms(coll)
        }
    }
}
</pre>
<a name="robo14"></a>
<a name="update5frobots2fcheck5fhealth"></a>
<h3>2.2.11.  update_robots/check_health [ procedure ]  </h3>
<p class="item_name">NAME</p>
<p>   check_health
</p>
<p class="item_name">DESCRIPTION</p>
<p>   check for robot health
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc check_health {} {
    global activeRobots data finish gui tick

    set num_rob  0
    set diffteam ""
    set num_team 0
    foreach robot $activeRobots {
        if {$data($robot,status)} {
            if {$data($robot,health) &lt;= 0 } {
                set data($robot,status) 0
                set data($robot,health) 0
                disable_robot $robot
                append finish "$data($robot,name) team($data($robot,team)) dead at tick: $tick\n"
                if {$gui} {
                    after 1 "show_die $robot"
                }
            } else {
                incr num_rob
                if {$data($robot,team) != ""} {
                    if {[lsearch -exact $diffteam $data($robot,team)] == -1} {
                        lappend diffteam $data($robot,team)
                        incr num_team
                    }
                } else {
                    lappend diffteam $data($robot,name)
                    incr num_team
                }
            }
        }
    }
    return [list $num_rob $diffteam $num_team]
}
</pre>
<a name="robo15"></a>
<a name="check5fhealth2fdisable5frobot"></a>
<h4>2.2.11.1.  check_health/disable_robot [ procedure ]  </h4>
<p class="item_name">NAME</p>
<p>   disable_robot
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc disable_robot {robot} {
    global activeRobots data tick

    if {[interp exists $data($robot,interp)]} {
        interp delete $data($robot,interp)
        set index [lsearch -exact $activeRobots $robot]
        set activeRobots [lreplace $activeRobots $index $index]
        array unset data $robot
    } else {
        display "disable robot $robot failed; interp does not exist"
    }
}
</pre>
<a name="robo16"></a>
<a name="run5frobots2ftick"></a>
<h2>2.3.  run_robots/tick [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   tick
</p>
<p class="item_name">DESCRIPTION</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc tick {} {
    global game tick

    if {$tick &lt; $game(max_ticks)} {
        incr tick
    } else {
        set game(state) "end"
    }
}
</pre>

