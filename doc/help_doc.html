<h3>TABLE OF CONTENTS</h3>
<ul>
<li>1. <a href="#robo0">help/file_header</a></li>
<li>2. <a href="#robo1">help/init_help</a></li>
<ul>
<li>2.1. <a href="#robo2">init_help/show_page</a></li>
<li>2.2. <a href="#robo3">init_help/end_help</a></li>
<li>2.3. <a href="#robo4">init_help/handle_click</a></li>
<li>2.4. <a href="#robo5">init_help/handle_scrollwheel</a></li>
</ul>
</ul>
<a name="robo0"></a>
<a name="help2ffile5fheader"></a>
<h1>1.  help/file_header [ file ]  </h1>
<p class="item_name">NAME</p>
<p>   help.tcl
</p>
<p class="item_name">DESCRIPTION</p>
<p>   This file contains the GUI description of the TclRobots help window.
</p>

<p>   The authors are Jonas Ferry, Peter Spjuth and Martin Lindskog, based
   on TclRobots 2.0 by Tom Poindexter.
</p>

<p>   See http://tclrobots.org for more information.
</p>
<p class="item_name">COPYRIGHT</p>
<p>   Jonas Ferry (jonas.ferry@tclrobots.org), 2010. Licensed under the
   Simplified BSD License. See LICENSE file for details.
</p>
<a name="robo1"></a>
<a name="help2finit5fhelp"></a>
<h1>2.  help/init_help [ procedure ]  </h1>
<p class="item_name">NAME</p>
<p>   init_help
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Displays the README file in a separate text window.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc init_help {} {
    # Create new toplevel and apply some settings
    toplevel .help
    grid columnconfigure .help 0 -weight 1; grid rowconfigure .help 0 -weight 1

    # Create navigation
    set ::helpnav_f [ttk::frame .help.f1]
    set ::help_b    [ttk::button $::helpnav_f.b1 -text "Show Help" \
                         -command {show_page help}]
    set ::license_b [ttk::button $::helpnav_f.b2 -text "Show License" \
                         -command {show_page license}]
    set ::close_b   [ttk::button $::helpnav_f.b3 -text "Close" \
                         -command {end_help}]

    # Grid the buttons
    grid $::help_b    -column 0 -row 0 -sticky w
    grid $::license_b -column 1 -row 0 -sticky w
    grid $::close_b   -column 2 -row 0 -sticky w

    # Load the HTML extension
    set ::html_help 0

    switch $::tcl_platform(platform) {
        windows {
            if {![catch {load $::thisDir/include/tkhtml/tkhtml.dll}] } {
                set ::html_help 1
            }
        }
        unix {
            if {![catch {load $::thisDir/include/tkhtml/tkhtml.so}] } {
                set ::html_help 1
            }
        }
    }
    if {$::html_help} {
        # HTML is enabled, create HTML widget
        set ::help_t [html .help.t -hyperlinkcommand handle_link \
                          -base $::thisDir/doc/game_doc.html \
                          -yscrollcommand ".help.s set"]

        # Bind mouse clicks
        bind .help &lt;ButtonRelease-1&gt; "handle_click %x %y"

        # Bind mouse scroll wheel
        if {[string equal "unix" $::tcl_platform(platform)]} {
            bind all &lt;4&gt; "+handle_scrollwheel %X %Y -1"
            bind all &lt;5&gt; "+handle_scrollwheel %X %Y 1"
        }
        bind all &lt;MouseWheel&gt; "+handle_scrollwheel %X %Y %D"

        # Read the HTML help doc
        set f [open $::thisDir/doc/game_doc.html]
        set ::help_text [read $f]
        close $f

        # Read the license doc, add &lt;pre&gt; formatting
        set f [open $::thisDir/LICENSE]
        set ::license_text "&lt;pre&gt;[read $f]&lt;/pre&gt;"
        close $f
    } else {
        # HTML is disabled, create text widget
        set ::help_t [tk::text .help.t -width 80 -height 30 -wrap word \
                        -yscrollcommand ".help.s set"]

        # Read the plain text help doc
        set f [open $::thisDir/README]
        set ::help_text [read $f]
        close $f

        # Read the license doc
        set f [open $::thisDir/LICENSE]
        set ::license_text [read $f]
        close $f
    }

    # Create scrollbar for help doc window
    set help_s [ttk::scrollbar .help.s -command ".help.t yview" \
                    -orient vertical]

    # Grid the text box and scrollbar
    grid $::helpnav_f -column 0 -row 0 -sticky nsew
    grid $::help_t    -column 0 -row 1 -sticky nsew
    grid $help_s      -column 1 -row 1 -sticky ns

    show_page help
}
</pre>
<a name="robo2"></a>
<a name="init5fhelp2fshow5fpage"></a>
<h2>2.1.  init_help/show_page [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   show_page
</p>
<p class="item_name">SYNOPSIS</p>
<p>   show_page page
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Show the requested page text in Help browser. Checks if HTML is
   enabled or not.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc show_page {page} {
    if {$page eq "help"} {
        set text $::help_text
    } else {
        set text $::license_text
    }
    if {$::html_help} {
        # Clear and insert new text into HTML widget
        $::help_t clear
        $::help_t parse $text
    } else {
        # Insert text into text box
        $::help_t delete 1.0 end
        $::help_t insert 1.0 $text
    }
}
</pre>
<a name="robo3"></a>
<a name="init5fhelp2fend5fhelp"></a>
<h2>2.2.  init_help/end_help [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   end_help
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Closes the help browser.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc end_help {} {
    destroy .help
}
</pre>
<a name="robo4"></a>
<a name="init5fhelp2fhandle5fclick"></a>
<h2>2.3.  init_help/handle_click [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   handle_click
</p>
<p class="item_name">SYNOPSIS</p>
<p>   handle_click x y
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Handle clicks on and off links in HTML widget. Called with the x and
   y coordinates of the click. Uses href widget command to figure out
   if a link was clicked.
</p>

<p>   Currently handles anchor links and the license link.
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc handle_click {x y} {
    # Must make sure that Close button has not destroyed the widget
    # before accessing it
    if {[winfo exists $::help_t]} {
        set link [$::help_t href $x $y]
        if {$link eq \
                "http://github.com/jonasferry/TclRobots/blob/master/LICENSE"} {
            show_page license
        } else {
            $::help_t yview [lindex [split $link #] 1]
        }
    }
}
</pre>
<a name="robo5"></a>
<a name="init5fhelp2fhandle5fscrollwheel"></a>
<h2>2.4.  init_help/handle_scrollwheel [ procedure ]  </h2>
<p class="item_name">NAME</p>
<p>   handle_scrollwheel
</p>
<p class="item_name">SYNOPSIS</p>
<p>   handle_click x y
</p>
<p class="item_name">DESCRIPTION</p>
<p>   Handles the mouse scrollwheel in the help browser.
</p>

<p>   From: http://code.activestate.com/recipes/68394/
</p>
<p class="item_name">SOURCE</p>
<pre class="source">proc handle_scrollwheel { x y delta } {

    # Find out what's the widget we're on
    set act 0
    set widget [winfo containing $x $y]

    if {$widget != ""} {
        # Make sure we've got a vertical scrollbar for this widget
        if {[catch "$widget cget -yscrollcommand" cmd]} return

        if {$cmd != ""} {
            # Find out the scrollbar widget we're using
            set scroller [lindex $cmd 0]

            # Make sure we act
            set act 1
        }
    }

    if {$act == 1} {
        # Now we know we have to process the wheel mouse event
        set xy [$widget yview]
        set factor [expr [lindex $xy 1]-[lindex $xy 0]]

        # Make sure we activate the scrollbar's command
        # The following line is original, but the second line works.
#        set cmd "[$scroller cget -command] scroll [expr -int($delta/(120*$factor))] units"
        set cmd "[$scroller cget -command] scroll $delta units"
        eval $cmd
    }
}
</pre>

