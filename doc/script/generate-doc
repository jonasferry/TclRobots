#!/usr/bin/env tclsh

proc main {} {
    set srcfiles  {tclrobots syscalls gui simulator}
    set ::srcdocs {}

    source ./doc/src/web.tcl

    foreach file $srcfiles {
        # Generate documentation for source code
        gen_src_doc ${file}.tcl ${file}_doc
        clean_file  ${file}_doc
    }
    gen_doc_overview

    # Generate documentation for robot API
    gen_api_doc syscalls.tcl api_doc
    clean_file  api_doc
}

proc gen_src_doc {srcfile docfile} {
    global srcdocs

    # Generate html
    exec robodoc --src $srcfile --doc ./doc/$docfile --singlefile \
        --html --sections --toc --rc ./doc/script/robodoc.rc --nosort \
        --headless --footless

    # Remove extra css file
    file delete ./doc/$docfile.css

    # Append file name for documentation overview page
    append srcdocs "<a href='${docfile}.html'>$docfile</a>\n"
}

proc gen_api_doc {srcfile docfile} {
    # Generate html
    exec robodoc --src $srcfile --doc ./doc/$docfile --singlefile \
        --html --sections --toc --rc ./doc/script/robodoc.rc --nosort \
        --headless --footless \
        --internalonly

    # Remove extra css file
    file delete ./doc/$docfile.css
}

proc clean_file {docfile} {
    # Clean up file
    set f [open ./doc/${docfile}.html]
    set toc 0
    set content 0
    set out {}

    foreach line [split [read $f] \n] {
        if {[regexp {TABLE OF CONTENTS} $line]} {
            # Start read table of contents. We don't want to remove
            # these links, as will be done with the content.
            set toc 1
        }
        if {$toc} {
            if {[regexp {<a name="robo0"></a>} $line]} {
                # Table of contents done
                set toc     0
                set content 1
            }
        }
        if {$content} {
            if {[regexp {<p>. <a href=.*>Top</a>.*} $line]} {
                #Remove navigation
                continue
            }
            # Remove <strong> and </strong>
            set line [regsub -all {</*strong>} $line {}]

            # Remove links
            set line [regsub -all {<a href=\"[^\"]+\">([^<]+)</a>} $line {\1}]
        }
        append out $line \n
    }
    close $f

    set f [open ./doc/${docfile}.html w]
    puts $f [header $docfile]
    puts $f $out
    puts $f [footer]
    close $f
}

proc gen_doc_overview {} {
    global srcdocs

    # Generate documentation overview page
    source ./doc/src/doc_overview.tcl
    set    f  [open ./doc/doc_overview.html w]
    puts   $f [header doc_overview]
    puts   $f [doc_overview $srcdocs]
    puts   $f [footer]
    close  $f
}

# All procedures are sourced; run main
main